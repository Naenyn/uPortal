plugins {
    // Top level plugins
    id 'com.github.kt3k.coveralls' version '2.12.2'
    id 'com.github.node-gradle.node' version '7.1.0'
    id 'net.researchgate.release' version '3.1.0'
    // Records all tasks in task graph, generates `build/reports/visteg.dot`
    // dot file can be converted to an image using graphviz. `dot -Tsvg -O -v visteg.dot`
    // id 'cz.malohlava.visteg' version '1.0.5'

    // Sub project plugins
    id 'com.github.sherter.google-java-format' version '0.9' apply false
    id 'nebula.lint' version '17.8.0' apply false
    id 'net.ltgt.errorprone' version '4.2.0' apply false
    id 'com.github.spotbugs' version '4.8.0' apply false
}

description = "Apereo uPortal $version"

/*======== Dependency Management ========**/
repositories {
    mavenCentral()
    mavenLocal()
}

node {
    version = nodejsVersion
    download = true
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'jacoco'
    apply plugin: 'eclipse'
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'idea'
    // Disabled for Java 17 compatibility
    // apply plugin: 'nebula.lint'
    apply plugin: 'groovy'
    apply plugin: 'codenarc'
    apply plugin: 'com.github.sherter.google-java-format'
    apply plugin: 'net.ltgt.errorprone'

    configurations {
        // Banned dependencies
        all*.exclude group: 'commons-logging', module: 'commons-logging'
        all*.exclude group: 'commons-logging', module: 'commons-logging-api'
        all*.exclude group: 'log4j', module: 'log4j'
        all*.exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }

    dependencies {
        errorprone 'com.google.errorprone:error_prone_core:2.18.0'
        // Removed errorproneJavac which is not needed for Java 17+
        compileOnly "${servletApiDependency}"
        compileOnly "org.springframework:spring-core:${springVersion}"
        compileOnly "org.slf4j:slf4j-api:${slf4jVersion}"
    }

    codenarc {
        toolVersion = '3.6.0'
        reportFormat = 'console'
    }

    // Version has to be set directly here, using a variable does not set Java compatability version
    sourceCompatibility = 17
    targetCompatibility = 17

    // Disabled gradleLint for Java 17 compatibility
    // gradleLint.criticalRules = [
    //   'overridden-dependency-version',
    //   'dependency-parentheses',
    //   'dependency-tuple',
    //   'empty-closure',
    //   'duplicate-dependency-class'
    // ]

    /*======== Static Analysis ========*/
    spotbugs {
        toolVersion = '4.9.3'
        // allow build to continue to other sub projects
        ignoreFailures = true
    }
    tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
        enabled = gradle.startParameter.taskNames.contains(it.name)
        reports {
            html {
                enabled = true
                destination = file("$buildDir/reports/spotbugs/main/spotbugs.html")
                stylesheet = 'fancy-hist.xsl'
            }
        }
    }
    googleJavaFormat {
        toolVersion '1.7'
        options style: 'AOSP'
        exclude '**/generated-sources'
        // Skip verification for Java 17 compatibility
        verifyGoogleJavaFormat.enabled = false
    }

    /*======== Aggregate Dependencies Report ========*/
    task dependenciesRecursive(type: DependencyReportTask) {
        group = 'Help'
        description = 'Provides a complete report on project dependencies;  use option (e.g.) --configuration compile to narrow'
    }

}

/*======== Child Projects ========*/
subprojects {

    /*======== Plugins ========*/
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    /*======== Dependency Management ========**/
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }

    dependencies {
        testImplementation "junit:junit:${junitVersion}"
        testImplementation "org.junit.jupiter:junit-jupiter-api:5.10.2"
        testImplementation "org.junit.jupiter:junit-jupiter-engine:5.10.2"
        testImplementation "org.easymock:easymock:${easymockVersion}"
        testImplementation "org.hamcrest:hamcrest-all:${hamcrestVersion}"
        testImplementation "org.mockito:mockito-core:${mockitoVersion}"
        testImplementation "org.springframework:spring-test:${springVersion}"
        testImplementation "org.xmlunit:xmlunit-legacy:${xmlunitVersion}"
        testImplementation "${servletApiDependency}"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.10.2"
        spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.14.0'
    }

    test {
        jvmArgs = [
            '--add-opens=java.base/java.lang=ALL-UNNAMED',
            '--add-opens=java.base/java.lang.reflect=ALL-UNNAMED',
            '--add-opens=java.base/java.util=ALL-UNNAMED',
            '--add-opens=java.xml/com.sun.org.apache.xerces.internal.jaxp=ALL-UNNAMED',
            '--add-opens=java.xml/com.sun.org.apache.xalan.internal.xsltc.trax=ALL-UNNAMED'
        ]
    }

    /* Release Management
     *
     * Based on Sonatype's guide for publishing w/ Gradle:  http://central.sonatype.org/pages/gradle.html
     */
    ext {
        publishingUsername = project.hasProperty('ossrhUsername') ? project.getProperty('ossrhUsername') : ""
        publishingPassword = project.hasProperty('ossrhPassword') ? project.getProperty('ossrhPassword') : ""
        publishingRepositoryUrl = project.hasProperty('ossrhUrl') ? project.getProperty('ossrhUrl') : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
        publishingRepositoryUrlSnapshot = project.hasProperty('ossrhUrlSnapshot') ? project.getProperty('ossrhUrlSnapshot') : "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    signing {
        required { gradle.taskGraph.hasTask('publish') }
        sign publishing.publications
    }
    tasks.withType(Javadoc).all {
        // disable JavaDocs until we start using them again in our website
        enabled = false
    }
    tasks.withType(Jar) {
        from(project.projectDir) {
            include "${rootDir}/LICENSE"
            into 'META-INF'
        }
    }
    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier = 'sources'
        from sourceSets.main.allSource
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }
    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier = 'javadoc'
        from javadoc.destinationDir
    }
    artifacts {
        archives javadocJar, sourcesJar
    }
    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                
                pom {
                    name = 'uPortal'
                    packaging = 'jar'
                    description = 'Enterprise open source portal built by and for the higher education community.'
                    url = 'https://github.com/uPortal-Project/uPortal'

                    scm {
                        connection = 'scm:git@github.com:uPortal-Project/uPortal.git'
                        url = 'https://github.com/uPortal-Project/uPortal'
                    }

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            organization = 'uPortal Developers'
                            organizationUrl = 'https://github.com/uPortal-Project/uPortal/graphs/contributors'
                        }
                    }
                }
            }
        }
        
        repositories {
            maven {
                name = 'OSSRH'
                url = version.endsWith('SNAPSHOT') ? publishingRepositoryUrlSnapshot : publishingRepositoryUrl
                credentials {
                    username = publishingUsername
                    password = publishingPassword
                }
            }
        }
    }
    // Use publish task for release builds
    afterReleaseBuild.dependsOn publish

    def jacocoExecutionDataFiles = fileTree(buildDir).include("/jacoco/*.exec")
    jacocoTestReport {
        getExecutionData().setFrom(jacocoExecutionDataFiles)
    }
}

/*
 * Configuration for the 'gradle-release' plugin (https://github.com/researchgate/gradle-release)
 */
release {
    tagTemplate = 'v${version}' // Looks like it should be a GString, but not necessary
    git {
        requireBranch.set('')
    }
}


task jacocoAggregateReport(type: org.gradle.testing.jacoco.tasks.JacocoReport) {
    dependsOn = subprojects.test
    getAdditionalSourceDirs().setFrom(files(subprojects.sourceSets.main.allSource.srcDirs))
    getSourceDirectories().setFrom(files(subprojects.sourceSets.main.allSource.srcDirs))
    getClassDirectories().setFrom(files(subprojects.sourceSets.main.output))
    getExecutionData().setFrom(files(subprojects.jacocoTestReport.executionData))

    reports {
        xml.required = true
        html.required = true
    }
    onlyIf = {
        true
    }
    doFirst {
        getExecutionData().setFrom(files(getExecutionData().findAll {
            it.exists()
        }))
    }
}

coveralls {
    jacocoReportPath = "${buildDir}/reports/jacoco/jacocoAggregateReport/jacocoAggregateReport.xml"
    sourceDirs = files(allprojects.sourceSets.main.allSource.srcDirs).files.absolutePath
}

// Root project dependencies
dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.10.2"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:5.10.2"
    testImplementation "org.mockito:mockito-core:5.10.0"
    testImplementation "${servletApiDependency}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.10.2"
}
